#@TYPE: Machine
#@NAME: Nvidia Jetson TX1
#@DESCRIPTION: Nvidia Jetson TX1 dev board

require conf/machine/include/arm/arch-armv8.inc
# workaround for package arch issue
PACKAGE_EXTRA_ARCHS_tune-armv7ahf-neon-vfpv4 = "${PACKAGE_EXTRA_ARCHS_tune-armv7ahf-neon} armv7ahf-vfp-vfpv4-neon"

DEFAULTTUNE = "aarch64"
require conf/multilib.conf
MULTILIBS = "multilib:lib32"
DEFAULTTUNE_virtclass-multilib-lib32 = "armv7ahf-neon-vfpv4"

PREFERRED_PROVIDER_virtual/kernel ?= "linux-tegra"
PREFERRED_PROVIDER_u-boot ?= "u-boot-tegra"
PREFERRED_PROVIDER_virtual/bootloader ?= "u-boot-tegra"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = "tegra210-jetson-tx1-p2597-2180-a01-devkit.dtb"

MACHINE_FEATURES = "alsa bluetooth ext2 ext3 vfat usbhost pci wifi rtc"
MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "tegra-firmware"
MACHINE_EXTRA_RRECOMMENDS += "kernel-devicetree tegra-firmware-brcm udev-rules-tegra kernel-module-bcmdhd"

UBOOT_MACHINE = "p2371-2180_defconfig"

SERIAL_CONSOLE = "115200 ttyS0"

SDCARD_ROOTFS ?= "${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.ext3"

GCCVERSION = "4.9%"

OLDEST_KERNEL_aarch64 = "3.10"
PREFERRED_VERSION_linux-libc-headers = "3.16"

PREFERRED_PROVIDER_virtual/libgl = "tegra-libraries"
PREFERRED_PROVIDER_virtual/egl = "tegra-libraries"
PREFERRED_PROVIDER_virtual/libgles1 = "tegra-libraries"
PREFERRED_PROVIDER_virtual/libgles2 = "tegra-libraries"

PREFERRED_PROVIDER_virtual/xserver = "xserver-xorg"
XSERVER = "xserver-xorg xf86-input-evdev xserver-xorg-video-nvidia xserver-xorg-module-libwfb"
MACHINE_GSTREAMER_1_0_PLUGIN = "lib32-gstreamer1.0-plugins-tegra"
PACKAGE_ARCH_pn-gstreamer1.0-meta-base = "${MACHINE_ARCH}"
PACKAGE_ARCH_pn-packagegroup-core-x11 = "${MACHINE_ARCH}"
PACKAGE_ARCH_pn-packagegroup-core-x11-base = "${MACHINE_ARCH}"

JETSON_USE_V4L2 ?= "0"
KERNEL_MODULE_AUTOLOAD = "${@['nvhost-vi', 'tegra_camera'][(d.getVar('JETSON_USE_V4L2', True) or '') == '1']}"
