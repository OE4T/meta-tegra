DESCRIPTION = "Generates DTB overlay containing UEFI certificate information for Tegra platforms"
LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://${COMMON_LICENSE_DIR}/MIT;md5=0835ade698e0bcf8506ecda2f7b4f302"

COMPATIBLE_MACHINE = "(tegra)"

# Add a bbappend to this recipe to supply a
# non-empty file continaing the key information,
# as generated by gen_uefi_default_keys_dts.sh
# in the L4T kit.
SRC_URI = "file://UefiDefaultSecurityKeys.dts"

inherit deploy nopackages

DEPENDS = "dtc-native"

B = "${WORKDIR}/build"

do_configure() {
    if [ ! -s "${WORKDIR}/UefiDefaultSecurityKeys.dts" ]; then
        bbfatal "Please provide a non-empty UefiDefaultSecurityKeys.dts"
    fi
}

do_compile() {
    dtc -Idts -Odtb -o ${B}/UefiDefaultSecurityKeys.dtbo ${WORKDIR}/UefiDefaultSecurityKeys.dts
}

do_install[noexec] = "1"

do_deploy() {
    install -d ${DEPLOYDIR}
    install -m 0644 ${B}/UefiDefaultSecurityKeys.dtbo ${DEPLOYDIR}/
}

addtask deploy before do_build after do_compile

PACKAGE_ARCH = "${MACHINE_ARCH}"
